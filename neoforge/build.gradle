plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    mavenCentral()

    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }

    maven { url 'https://mvnrepository.com/artifact/me.shedaniel.cloth/cloth-config-fabric'}
    maven { url 'https://maven.architectury.dev/'}

    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = uri("https://api.modrinth.com/maven")
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }

    exclusiveContent {
        forRepository {
            maven {
                name = "Terraformers"
                url = uri("https://maven.terraformersmc.com/")
            }
        }
        filter {
            includeGroup "com.terraformersmc"
        }
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modImplementation "com.terraformersmc:modmenu:${property("modmenu_version")}"
    modImplementation 'me.shedaniel.cloth:cloth-config-neoforge:19.0.147'
    include 'me.shedaniel.cloth:cloth-config-neoforge:19.0.147'
    include "com.terraformersmc:modmenu:${property("modmenu_version")}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
